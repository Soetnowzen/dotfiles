#!/bin/tcsh

set autoexpand

# Aliases
# {
alias ls ls -F --color --group-directories-first
alias la ls -A
alias ll la -l
alias .. cd ..
alias :q exit
alias c cat -nv
alias clr clear
alias fi "find \!* | grep '[^/]*'\"\$"''"
# " to escape $ I need to '\"\$"'
alias g git
alias g_pl_stash 'git stash && git pull --rebase && git stash pop'
alias grep grep --color
alias grepc grep -Rin --color --include=*.{cc,h}
alias h history
alias less less -r
alias mkdir mkdir -pv
alias print_path 'echo $PATH | tr : "\n"'
alias psu ps -U \$USER
alias rm rm -I
alias tm 'tmux attach || tmux new'
alias v vim
alias v-split vim -o
alias v-tsplit vim -p
alias v-vsplit vim -O
alias vimr vim ~/.vimrc
# }

# Colors
# {
set color
setenv LS_COLORS "di=0;35:ln=0;36:ex=0;33:pi=0;32:so=0;31:bd=0;37:mi=0;36:cd=1;35:tw=0;30:ow=0;34:"
setenv TERM "xterm-256color"

# alias color_failure grep -i \'err\\w*\\\|warn\\w*\\\|info\\w*\\\|fail\\w*\\\|\$\'
set RESET="%{\033[0m%}"
set RED="%{\033[31m%}"
set GREEN="%{\033[32m%}"
set YELLOW="%{\033[33m%}"
set BLUE="%{\033[34m%}"
set MAGENTA="%{\033[35m%}"
set CYAN="%{\033[36m%}"
set WHITE="%{\033[37m%}"
# set VIOLET="%{\033[313m%}"
# set BLACK="%{\033[316m%}"
# }

# Prompt
# {
alias get_number_of_git_changes "git status -s -uno |& grep '^\s*M' | wc -l"
alias print_addition_if_not_zero '[ \!:1 != 0 ] && echo "+\!:1" || echo ""'
alias print_number_of_git_changes 'print_addition_if_not_zero `get_number_of_git_changes`'
alias get_current_git_branch "git branch |& grep \* | sed 's/\* \(.*\)/\1/'"
alias get_current_tag "git tag -l --points-at HEAD"
alias precmd 'set exit_code=" $?"; \\
  set branch=`get_current_git_branch`; \\
    set git_changes=`print_number_of_git_changes`; \\
      set tag=`get_current_tag`; \\
        if "$exit_code" == " 0" set exit_code=""; \\
          if "$git_changes" != "" set tag="$tag, "; \\
            if "$tag" != "" set branch="$branch, "'
set prompt='[%{\033[36m%}%T %{\033[34m%}%n%{\033[0m%}@%{\033[34m%}%m %{\033[32m%}%~%{\033[33m%}(%$branch%{\033[37m%}%$tag%{\033[35m%}%$git_changes%{\033[33m%})%{\033[31m%}%$exit_code%{\033[0m%}]%# '
# }

# bindkey -v

# Auto complete searches when using up and down
bindkey "^[[A" history-search-backward # ]
bindkey "^[[B" history-search-forward # ]

if (-r ~/.cshrc.work) source ~/.cshrc.work

